# --- Setup ---

self := "setup"
import "../prelude/task/justfile"
_default: help

# --- Tasks ---

all:
  just setup brew
  just setup home
  just setup brew-services
  just setup mac-dock
  just setup mac-defaults
  just setup nvm
  just setup rust
  just setup arc

# Setup home directory, linking appropriate dotfiles.
home:
  #!/usr/bin/env bash
  set -euo pipefail

  # Stow dotfiles.
  ls -A links | grep -v "^\.\+$" | xargs stow -d ./links -t "$HOME"

# Install desired brew packages.
brew:
  #!/usr/bin/env bash
  set -euo pipefail

  # Bitwarden password manager.
  brew install --cask bitwarden

  # Arc browser.
  brew install --cask arc

  # WezTerm terminal.
  brew install --cask wezterm

  # Warp terminal.
  brew install --cask warp

  # JetBrains Mono Nerd Font.
  brew install --cask font-jetbrains-mono-nerd-font

  # Tiling window manager.
  # - https://github.com/nikitabobko/AeroSpace
  brew install --cask nikitabobko/tap/aerospace

  # Alfred productivity app.
  # - https://www.alfredapp.com/
  brew install --cask alfred

  # Top bar.
  brew install --cask jordanbaird-ice

  # dockutil: Dock utility to manage macOS dock items.
  # - https://formulae.brew.sh/formula/dockutil
  # - https://github.com/kcrawford/dockutil

  # Entire list of normal brew packages.
  brew install \
    bat \
    bufbuild/buf/buf \
    cmake \
    coreutils \
    cowsay \
    dockutil \
    eza \
    fd \
    felixkratz/formulae/borders \
    ffmpeg \
    fzf \
    gh \
    glow \
    go \
    goose \
    hashicorp/tap/terraform \
    helm \
    htop \
    imagemagick \
    jq \
    kind \
    livekit-cli \
    md5sha1sum \
    mosh \
    neofetch \
    neovim \
    opentofu \
    postgresql@17 \
    pulumi/tap/pulumi \
    python@3.13 \
    redis \
    ripgrep \
    ruff \
    shellcheck \
    steveyegge/beads/bd \
    stow \
    the_silver_searcher \
    tilt-dev/tap/ctlptl \
    tilt-dev/tap/tilt \
    tmux \
    tree \
    uv \
    wget \
    withgraphite/tap/graphite \
    yq \
    zoxide \
    zsh-autosuggestions \
    zsh-syntax-highlighting

# Start desired brew services.
brew-services:
  #!/usr/bin/env bash
  set -euo pipefail

  # Start services.
  brew services start postgresql@17
  brew services start felixkratz/formulae/borders

# Install Rust programming language.
rust:
  #!/usr/bin/env bash
  set -euo pipefail

  # Install rust via rustup.
  # Check if rustup is already installed.
  if command -v rustup &> /dev/null; then
    echo "> rustup is already installed, skipping."
  else
    curl --proto '=https' --tlsv1.2 -sSf https://sh.rustup.rs | sh
  fi

# Install nvm (Node Version Manager).
nvm:
  #!/usr/bin/env bash
  set -euo pipefail

  # Check if nvm is installed
  if [ -f "$HOME/.nvm/nvm.sh" ]; then
    echo "> nvm is already installed, skipping."
  else
    # Set profile to null to prevent nvm from trying to modify shell profile.
    PROFILE=/dev/null bash -c 'curl -o- https://raw.githubusercontent.com/nvm-sh/nvm/v0.40.3/install.sh | bash'
  fi

# Manual steps needed for arc.
arc:
  #!/usr/bin/env bash
  set -euo pipefail

  # Action items.
  # - [ ] Login.
  # - [ ] Settings -> Kyle -> Sync sidebar.
  # - [ ] Settings -> Profiles -> Make: Personal, Stoke, Ragkit.
  # - [ ] Settings -> Profiles -> Set as Default
  # - [ ] Settings -> Profiles -> Archive Tabs after 30 days
  # - [ ] Settings -> Profiles -> Downloads -> Ask Every Time
  # - [ ] Settings -> Arc -> Set as Default Browser
  # - [ ] On Each Profile -> Passwords -> Settings -> Disable Offer to Save Passwords

# Configures the mac dock.
mac-dock:
  #!/usr/bin/env bash
  set -euo pipefail

  # TODO: Check that dockutil is installed. Say to use brew install if not.
  DOCK_ITEMS=$(dockutil --list | awk -F'file:///' '{gsub(/[[:space:]]+$/, "", $1); print $1}')

  try_remove_dock_item() {
    if echo "$DOCK_ITEMS" | grep -Fxq "$1"; then
      dockutil --remove "$1" --no-restart
    fi
  }


  # Keep: Finder, System Settings, Downloads, Trash.
  try_remove_dock_item "Launchpad"
  try_remove_dock_item "Messages"
  try_remove_dock_item "Safari"
  try_remove_dock_item "Mail"
  try_remove_dock_item "Maps"
  try_remove_dock_item "Photos"
  try_remove_dock_item "FaceTime"
  try_remove_dock_item "Calendar"
  try_remove_dock_item "Contacts"
  try_remove_dock_item "Reminders"
  try_remove_dock_item "Notes"
  try_remove_dock_item "Freeform"
  try_remove_dock_item "TV"
  try_remove_dock_item "Music"
  try_remove_dock_item "News"
  try_remove_dock_item "Keynote"
  try_remove_dock_item "Numbers"
  try_remove_dock_item "Pages"
  try_remove_dock_item "App Store"

  # Finally, remove spacer tiles and do a restart.
  dockutil --remove spacer-tiles --restart

# Sets macOS defaults to desired values.
mac-defaults:
  #!/usr/bin/env bash
  set -euo pipefail

  echo "Setting macOS defaults..."

  # Usage: ensure_defaults "com.apple.dock" "orientation" "-string" "left"
  ensure_defaults() {
    local domain="$1"
    local key="$2"
    local type="$3"
    local value="$4"

    # Get current value (redirect stderr to avoid error if key does not exist)
    local current_value
    current_value=$(defaults read "$domain" "$key" 2>/dev/null || echo "null")

    # Check if the current value matches the desired value
    if [[ "$current_value" != "$value" ]]; then
      # Set the value and echo message
      echo "--> Setting $domain $key to '$value'"
      defaults write "$domain" "$key" "$type" "$value"
    fi
  }

  # Dock on left.
  ensure_defaults "com.apple.dock" "orientation" "-string" "left"
  # Make icons smaller.
  ensure_defaults "com.apple.dock" "tilesize" "-int" "36"
  # Auto-hide the Dock.
  ensure_defaults "com.apple.dock" "autohide" "-bool" "true"
  # Make the hide animation instant.
  ensure_defaults "com.apple.dock" "autohide-time-modifier" "-float" "0"
  # Do not show recent applications.
  ensure_defaults "com.apple.dock" "show-recents" "-bool" "false"
  # Reduce the minimize animation.
  ensure_defaults "com.apple.dock" "mineffect" "-string" "scale"
  # Do not rearrange spaces automatically.
  ensure_defaults "com.apple.dock" "mru-spaces" "-bool" "false"

  # Update screenshots save location.
  mkdir -p "~/Screenshots"
  ensure_defaults "com.apple.screencapture" "location" "-string" "~/Screenshots"
  # Do not show thumbnail after taking screenshot.
  ensure_defaults "com.apple.screencapture" "show-thumbnail" "-bool" "false"

  # Show extensions in finder.
  ensure_defaults "NSGlobalDomain" "AppleShowAllExtensions" "-bool" "true"
  # Show hidden files in finder.
  ensure_defaults "com.apple.finder" "AppleShowAllFiles" "-bool" "false"
  # Show path bar.
  ensure_defaults "com.apple.finder" "ShowPathbar" "-bool" "true"
  # Set columns as the default view.
  ensure_defaults "com.apple.finder" "FXPreferredViewStyle" "-string" "clmv"
  # Sort folders first.
  ensure_defaults "com.apple.finder" "_FXSortFoldersFirst" "-bool" "true"
  # Do not show warning when changing file extension.
  ensure_defaults "com.apple.finder" "FXEnableExtensionChangeWarning" "-bool" "false"
  # Save locally by default, not in cloud.
  ensure_defaults "NSGlobalDomain" "NSDocumentSaveNewDocumentsToCloud" "-bool" "false"
  # Hide all icons on desktop.
  ensure_defaults "com.apple.finder" "CreateDesktop" "-bool" "false"

  # Disable apple intelligence. Needs a restart to take effect.
  ensure_defaults "com.apple.CloudSubscriptionFeatures.optIn" "545129924" "-bool" "false"

  echo "All defaults set, restarting affected services"

  # Restart services.
  killall Dock
  killall Finder
  killall SystemUIServer

  echo "Done. Some changes may require a logout/restart to take effect."
