# --- Setup ---

self := "setup"
import "../prelude/task/justfile"
_default: help

# --- Tasks ---

# Install desired brew packages.
brew:
  #!/usr/bin/env bash
  set -euo pipefail

  # Bitwarden password manager.
  brew install --cask bitwarden

  # Arc browser.
  brew install --cask arc

  # Warp terminal.
  brew install --cask warp

  # Dock utility to manage macOS dock items.
  # - https://formulae.brew.sh/formula/dockutil
  # - https://github.com/kcrawford/dockutil
  brew install dockutil

# Manual steps needed for arc.
arc:
  #!/usr/bin/env bash
  set -euo pipefail

  # Action items.
  # - [ ] Login.
  # - [ ] Settings -> Kyle -> Sync sidebar.
  # - [ ] Settings -> Profiles -> Make: Personal, Stoke, Ragkit.
  # - [ ] Settings -> Profiles -> Set as Default
  # - [ ] Settings -> Profiles -> Archive Tabs after 30 days
  # - [ ] Settings -> Profiles -> Downloads -> Ask Every Time
  # - [ ] Settings -> Arc -> Set as Default Browser
  # - [ ] On Each Profile -> Passwords -> Settings -> Disable Offer to Save Passwords

# Configures the mac dock.
mac-dock:
  #!/usr/bin/env bash
  set -euo pipefail

  # TODO: Check that dockutil is installed. Say to use brew install if not.
  DOCK_ITEMS=$(dockutil --list | awk -F'file:///' '{gsub(/[[:space:]]+$/, "", $1); print $1}')

  try_remove_dock_item() {
    if echo "$DOCK_ITEMS" | grep -Fxq "$1"; then
      dockutil --remove "$1" --no-restart
    fi
  }


  # Keep: Finder, System Settings, Downloads, Trash.
  try_remove_dock_item "Launchpad"
  try_remove_dock_item "Messages"
  try_remove_dock_item "Safari"
  try_remove_dock_item "Mail"
  try_remove_dock_item "Maps"
  try_remove_dock_item "Photos"
  try_remove_dock_item "FaceTime"
  try_remove_dock_item "Calendar"
  try_remove_dock_item "Contacts"
  try_remove_dock_item "Reminders"
  try_remove_dock_item "Notes"
  try_remove_dock_item "Freeform"
  try_remove_dock_item "TV"
  try_remove_dock_item "Music"
  try_remove_dock_item "News"
  try_remove_dock_item "Keynote"
  try_remove_dock_item "Numbers"
  try_remove_dock_item "Pages"
  try_remove_dock_item "App Store"

  # Finally, remove spacer tiles and do a restart.
  dockutil --remove spacer-tiles --restart

# Sets macOS defaults to desired values.
mac-defaults:
  #!/usr/bin/env bash
  set -euo pipefail

  # Usage: ensure_defaults "com.apple.dock" "orientation" -string "left"
  ensure_defaults() {
    # Validate that all 4 parameters were provided
    if [[ $# -ne 4 ]]; then
      abort "ensure_defaults_string requires 4 parameters: domain, key, type, value"
    fi

    local domain="$1"
    local key="$2"
    local type="$3"
    local value="$4"

    # Get current value (redirect stderr to avoid error if key does not exist)
    local current_value
    current_value=$(defaults read "$domain" "$key" 2>/dev/null)

    # Check if the current value matches the desired value
    if [[ "$current_value" != "$value" ]]; then
      # Set the value and echo message
      ohai "Setting $domain $key to '$value'"
      defaults write "$domain" "$key" "$type" "$value"
    fi
  }

  # Dock on left.
  ensure_defaults "com.apple.dock" "orientation" -string "left"
  # Make icons smaller.
  ensure_defaults "com.apple.dock" "tilesize" -int "36"
  # Auto-hide the Dock.
  ensure_defaults "com.apple.dock" "autohide" -bool "true"
  # Make the hide animation instant.
  ensure_defaults "com.apple.dock" "autohide-time-modifier" -float "0"
  # Do not show recent applications.
  ensure_defaults "com.apple.dock" "show-recents" -bool "false"
  # Reduce the minimize animation.
  ensure_defaults "com.apple.dock" "mineffect" -string "scale"
  # Do not rearrange spaces automatically.
  ensure_defaults "com.apple.dock" "mru-spaces" -bool "false"

  # Update screenshots save location.
  mkdir -p "~/Screenshots"
  ensure_defaults "com.apple.screencapture" "location" -string "~/Screenshots"
  # Do not show thumbnail after taking screenshot.
  ensure_defaults "com.apple.screencapture" "show-thumbnail" -bool "false"

  # Show extensions in finder.
  ensure_defaults "NSGlobalDomain" "AppleShowAllExtensions" -bool "true"
  # Show hidden files in finder.
  ensure_defaults "com.apple.finder" "AppleShowAllFiles" -bool "false"
  # Show path bar.
  ensure_defaults "com.apple.finder" "ShowPathbar" -bool "true"
  # Set columns as the default view.
  ensure_defaults "com.apple.finder" "FXPreferredViewStyle" -string "clmv"
  # Sort folders first.
  ensure_defaults "com.apple.finder" "_FXSortFoldersFirst" -bool "true"
  # Do not show warning when changing file extension.
  ensure_defaults "com.apple.finder" "FXEnableExtensionChangeWarning" -bool "false"
  # Save locally by default, not in cloud.
  ensure_defaults "NSGlobalDomain" "NSDocumentSaveNewDocumentsToCloud" -bool "false"
  # Hide all icons on desktop.
  ensure_defaults "com.apple.finder" "CreateDesktop" -bool "false"

  # Disable apple intelligence. Needs a restart to take effect.
  ensure_defaults "com.apple.CloudSubscriptionFeatures.optIn" "545129924" -bool "false"

  # Restart services.
  killall Dock
  killall Finder
  killall SystemUIServer
